MODULE TournamentTable;

REQUIRE GameModule, TeamModule;

hostGamesPlayed = GROUP SUM 1 BY hostTeam(Game game);
guestGamesPlayed = GROUP SUM 1 BY guestTeam(Game game);
gamesPlayed 'И' (Team team) = OVERRIDE hostGamesPlayed(team) (+) guestGamesPlayed(team), 0;

gamesWonBy(Team team, GameResult type) = OVERRIDE [GROUP SUM 1 BY winner(Game game), result(game)](team, type),
    0 IF team IS Team AND type IS GameResult MATERIALIZED;

gamesWon 'В' (Team team) = gamesWonBy(team, GameResult.win);
gamesWonOT 'ВО' (Team team) = gamesWonBy(team, GameResult.winOT);
gamesWonSO 'ВБ' (Team team) = gamesWonBy(team, GameResult.winSO);

gamesLostBy(Team team, GameResult type) = OVERRIDE [GROUP SUM 1 BY looser(Game game), result(game)](team, type),
    0 IF team IS Team AND type IS GameResult MATERIALIZED;

gamesLost 'П' (Team team) = gamesLostBy(team, GameResult.win);
gamesLostOT 'ПО' (Team team) = gamesLostBy(team, GameResult.winOT);
gamesLostSO 'ПБ' (Team team) = gamesLostBy(team, GameResult.winSO);

points 'Очки' (Team team) = gamesWon(team) * 3 + (gamesWonSO(team) + gamesWonOT(team)) * 2 + gamesLostOT(team) + gamesLostSO(team);

hostGoalsScored = GROUP SUM hostGoals(Game game) BY hostTeam(game);
guestGoalsScored = GROUP SUM guestGoals(Game game) BY guestTeam(game);
goalsScored 'Кол-во забитых голов' (Team team) = OVERRIDE hostGoalsScored(team) (+) guestGoalsScored(team), 0 IF team IS Team;

hostGoalsConceded = GROUP SUM guestGoals(Game game) BY hostTeam(game);
guestGoalsConceded = GROUP SUM hostGoals(Game game) BY guestTeam(game);
goalsConceded 'Кол-во пропущенных голов' (Team team) =
    OVERRIDE hostGoalsConceded(team) (+) guestGoalsConceded(team), 0 IF team IS Team;

place 'Место' (Team team) = 
    PARTITION SUM 1 ORDER DESC points(team), gamesPlayed(team), gamesWon(team), gamesWonOT(team), gamesWonSO(team),
    (OVERRIDE goalsScored(team) (-) goalsConceded(team), 0), goalsScored(team);

//-------------------- Logic of the overall tournament scorers list --------------------------------------
goalsPerPlayerCount 'Забито голов' (Player p) = GROUP SUM 1 IF p = scorer(ActivePlayers ap) ;
playedGamesPerPlayer 'Сыграно игр' (Player p) = GROUP SUM 1 IF p == scorer(ActivePlayers ap) 
    AND playerTeam(p) == playerTeam(scorer(ap));
goalsConcededByPLayersTeam 'Пропущено голов командой' (Player p) = goalsConceded(playerTeam(p));
playersPoints 'Очки' (Player p)= playedGamesPerPlayer(p) * 40 (-) goalsPerPlayerCount(p) (-) goalsConcededByPLayersTeam(p);
scorerPlace 'Место' (Player p) = PARTITION SUM 1 IF goalsPerPlayerCount(p) > 0
    ORDER DESC playersPoints(p), goalsPerPlayerCount(p), playedGamesPerPlayer(p), goalsConcededByPLayersTeam(p),  p;


FORM scoreTable 'Турнирная таблица'
    OBJECTS team = Team
    PROPERTIES(team) place, teamName, gamesPlayed, gamesWon, gamesWonOT, gamesWonSO,
        gamesLostSO, gamesLostOT, gamesLost, goalsScored, goalsConceded, points, NEW, DELETE
    ORDERS place(team)
;

//------------------------------- FORM BODY FROM GameModule ----------------------------------------------
EXTEND FORM scoreTable
    OBJECTS g = Game
    PROPERTIES(g) date, hostTeamName, guestTeamName, hostGoals, guestGoals, resultName, winnerTeamName
    PROPERTIES(g) PANEL remainingHostGoals, remainingGuestGoals
    PROPERTIES(g) NEW, EDIT, DELETE

    OBJECTS p = Player
    FILTERS playerTeam(p) == guestTeam(g) OR playerTeam(p) == hostTeam(g)

    OBJECTS ap = ActivePlayers
    PROPERTIES(ap) scorer
    PROPERTIES(ap) READONLY activePlayerNumber, activePlayerFullName, activePlayerTeamStatus
    PROPERTIES(ap) NEW, DELETE
    FILTERS gameOfScorer(ap) == g
;

//------------------------------- FORM BODY FROM TeamModule -----------------------------------------------
EXTEND FORM scoreTable
    OBJECTS t = Team
    PROPERTIES(t) teamName, teamPLayersLimit, NEW, EDIT, DELETE

    OBJECTS pl = Player
    PROPERTIES(pl) playerLastName, playerFirstName, playerNumber, playerTeamName
    PROPERTIES(pl) NEW, EDIT, DELETE
    FILTERS playerTeam(pl) == t;
;

//--------------------------- Overall tournament scorers list  FORM -----------------------------------------
EXTEND FORM scoreTable
    OBJECTS activePlayesrs = ActivePlayers
    LIST ActivePlayers OBJECT activePlayesrs
    
    OBJECTS scorers = Player
    PROPERTIES(scorers) READONLY scorerPlace, playerFirstName, playerLastName, playerNumber, playerTeamName, 
        playedGamesPerPlayer, goalsPerPlayerCount, goalsConcededByPLayersTeam, playersPoints
    FILTERS goalsPerPlayerCount(scorers) > 0
    ORDERS scorerPlace(scorers)
;

DESIGN scoreTable {
    NEW gameAndTeamDetailsContainer {
        tabbed = TRUE;
        fill = 1;
        NEW teamDetailsContainer {
            caption = 'Команды и их составы';
            horizontal = TRUE;
            MOVE BOX(t) {
                caption = 'Создание команды';
            };
            MOVE BOX(pl) {
                caption = 'Добавление игроков в команду';
            };
        }
        NEW gameDetailContainer {
            caption = 'Детали матчей';
            horizontal = TRUE;
            fill = 1;
            defaultComponent = TRUE;
            MOVE BOX(g) {;
                height= 350;
                width = 900;
                caption = 'Матчи';
            }
            MOVE BOX(ap) {
                height = 300;
                fill = 1;
                caption = 'список бомбардиров для каждого матча';

                MOVE PROPERTY (remainingHostGoals(g)){
                    toolTip = 'Этот показатель нужен для контроля кол-ва голов для хозяев, введеных вами в  таблице бомбардиров и кол-ва голов, которые забили хозяева в матче';
                    background = #bcd2ff;
                    foreground = #ff0627;
                };
                MOVE PROPERTY (remainingGuestGoals(g)){
                    background = #bcd2ff;
                    foreground = #ff0627;
                    toolTip = 'Этот показатель нужен для контроля кол-ва голов для гостей, введеных вами в  таблице бомбардиров и кол-ва голов, которые забили гости в матче';
                };
            }
        
        }
        NEW scorersListContainer {
            caption = 'Рейтинг бомбардиров турнира';
            MOVE BOX(scorers);
        }
    }
    
    NEW tournamentTableContainer {
        fill = 1;
        caption = 'Турнирная таблица';
        collapsed = FALSE;
        MOVE BOX(team);
    }
} 

NAVIGATOR {
    NEW scoreTable;
    NEW teamPlayersTable;
    NEW gameDetails;
}
