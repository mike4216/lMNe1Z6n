MODULE GameModule;

REQUIRE TeamModule;

CLASS GameResult 'Р/И' {
    win     'П',
    winOT   'ПО',
    winSO   'ПБ'
}

// ---------------------------- class Game start START------------------------------------- 
CLASS Game 'Игра';
date 'Дата'     = DATA DATE (Game);
hostTeam        = DATA Team (Game);
guestTeam       = DATA Team (Game);
hostGoals 'Х голы' = DATA INTEGER (Game);
guestGoals 'Г голы' = DATA INTEGER (Game);

hostTeamName 'Хозяева' (Game game) = teamName(hostTeam(game));
guestTeamName 'Гости' (Game game) = teamName(guestTeam(game));

CONSTRAINT hostTeam(Game team) = guestTeam(team) CHECKED BY hostTeam, guestTeam
    MESSAGE 'Хозяйская и гостевая команды должны быть разными';

CONSTRAINT hostGoals(Game game) = guestGoals(game) MESSAGE 'Игра не может закончиться вничью';

winner(Game game) = IF hostGoals(game) > guestGoals(game)
    THEN hostTeam(game)
    ELSE guestTeam(game);

looser(Game game) = IF hostGoals(game) > guestGoals(game)
    THEN guestTeam(game)
    ELSE hostTeam(game);

resultName 'Имя' (GameResult game) = staticCaption(game) IF game IS GameResult IN base;

userResult = DATA GameResult (Game);

result (Game game) = OVERRIDE userResult(game),
    (GameResult.win IF ((hostGoals(game) (-) guestGoals(game)) > 1 OR (guestGoals(game) (-) hostGoals(game)) > 1));

resultName 'Р/И' (Game game) = resultName(result(game));

CONSTRAINT
    ((hostGoals(Game game) (-) guestGoals(game)) < 2 AND (hostGoals(game) (-) guestGoals(game)) > -2) 
        AND NOT userResult(game)
    MESSAGE 'Укажите результат игры';

totalGoals 'Общая сумма голов' (Game g) = hostGoals(g) + guestGoals(g);
//------------------------------------ class Game END ------------------------------------- 

// ------------------------------ class ActivePlayer START -------------------------------- 
CLASS ActivePlayers 'Бомбардиры';
gameOfScorer   'Игра'           = DATA Game (ActivePlayers);
scorer         'Автор гола'     = DATA Player (ActivePlayers);

activePlayerFullName 'Полное имя' (ActivePlayers ap) =
    playerFirstName(scorer(ap)) + ' ' + playerLastName(scorer(ap));
activePlayerNumber 'Номер игрока' (ActivePlayers ap) = playerNumber(scorer(ap));
activePlayerTeamStatus 'Команда' (ActivePlayers ap) =
    IF playerTeam(scorer(ap)) == hostTeam(gameOfScorer(ap))
    THEN 'Хозяева'
    ELSE 'Гости';

activePlayersGoalsCount 'Записи о голах' (Game g) =
    GROUP SUM 1 IF gameOfScorer(ActivePlayers ap) == g;

isGoalsCountValid(Game g) = activePlayersGoalsCount(g)!= totalGoals(g);

CONSTRAINT isGoalsCountValid(Game g)
    MESSAGE 'Общая сумма голов в матче не совпадает с количеством голов в таблице бомбардиров';
// ------------------------------- class ActivePlayer END --------------------------------- 


FORM gameDetails 'Детали игры'
    OBJECTS g = Game
    PROPERTIES(g) isGoalsCountValid ,activePlayersGoalsCount, totalGoals, date, hostTeamName, guestTeamName, hostGoals, guestGoals, resultName, winner, looser
    PROPERTIES(g) NEW, EDIT, DELETE
        
    OBJECTS p = Player
    FILTERS playerTeam(p) == guestTeam(g) OR playerTeam(p) == hostTeam(g)
    
    OBJECTS ap = ActivePlayers 
    PROPERTIES(ap) scorer
    PROPERTIES(ap) READONLY activePlayerNumber, activePlayerFullName, activePlayerTeamStatus
    PROPERTIES(ap) NEW, DELETE
    FILTERS gameOfScorer(ap) == g
;

DESIGN gameDetails {
    NEW gameDetailsContainer {
        horizontal = TRUE;
        caption = 'Детали матчей';
        MOVE BOX(g) {;
            height= 350;
            width = 1000;
            caption = 'Матчи';
        }

        MOVE BOX(ap) {
            width = 600;
            caption = 'список бомбардиров для каждого матча';
        }
    }
}

NAVIGATOR {
    NEW gameDetails;
}
